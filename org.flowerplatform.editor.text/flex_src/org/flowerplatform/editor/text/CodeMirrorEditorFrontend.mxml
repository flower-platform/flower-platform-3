<?xml version="1.0" encoding="utf-8"?>
<!-- 
license-start

Copyright (C) 2008 - 2013 Crispico, <http://www.crispico.com/>.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation version 3.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details, at <http://www.gnu.org/licenses/>.

Contributors:
Crispico - Initial API and implementation  The product is licensed under GPL. This is the new version of the license.

license-end
-->
<!--
	@author Cristina Constantinescu
-->
<editorsupport:EditorFrontend
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:texteditor="com.crispico.flower.texteditor.*"
	xmlns:editor="com.crispico.flower.mp.web.texteditor.*"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:editorsupport="org.flowerplatform.editor.*"
	xmlns:iframe="org.flowerplatform.flexutil.iframe.*" xmlns:ns="org.flowerplatform.editor.text.javascript_view.*"
	initialize="initializeHandler(event)">
	
	<mx:Script>	
		<![CDATA[
			import com.crispico.flower.texteditor.TextChangedEventBuffer;
			import com.crispico.flower.texteditor.events.BufferedTextChangesEvent;
			import com.crispico.flower.texteditor.events.TextChangedEvent;
			
			import mx.collections.ArrayCollection;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			import mx.utils.UIDUtil;
			
			import org.flowerplatform.communication.CommunicationPlugin;
			import org.flowerplatform.editor.remote.EditorStatefulClient;
			import org.flowerplatform.editor.text.codemirror_editor.ICodeMirrorEditor;
			import org.flowerplatform.editor.text.codemirror_editor.MobileCodeMirrorEditor;
			import org.flowerplatform.editor.text.codemirror_editor.WebCodeMirrorEditor;
			import org.flowerplatform.editor.text.remote.CodeMirrorEditorStatefulClient;
			import org.flowerplatform.editor.text.remote.TextEditorUpdate;
			import org.flowerplatform.flexutil.FlexUtilGlobals;
			
			private var content:String;
			private var readOnly:Boolean;	
			private var creationComplete:Boolean;
			
			/**
			 * Stores a codeMirror editor frontend id.
			 * Used in flex - javascript communication to identify the right editor
			 * when executing a function on flex side.
			 * @see textEditorChangedHandler
			 */ 
			public var frontendId:String = UIDUtil.createUID();
			
			private static const EDITOR_PAGE:String = "codemirror/codeMirrorEditor.html";
			
			private static const CLIENT_KEYSTROKE_AGGREGATION_INTERVAL:String = "codemirror.client.keystroke.aggregation.interval";
			
			private static var _keystrokeAggregationInterval:int = 3000;			
			
			public var keystrokeBuffer:TextChangedEventBuffer;
					
			public function setKeystrokeAggregationInterval(interval:int = 0):void {
				if (keystrokeBuffer)
					keystrokeBuffer.setInterval(interval);
				else
					keystrokeBuffer = new TextChangedEventBuffer(this, interval);
			}			
			
			/**
			 * Changes the dirty state for the keystroke aggregation buffer.
			 */ 
			public function updateDirtyState(dirtyState:Boolean):void {
				setKeystrokeAggregationBufferDirtyState(dirtyState);
			}
			
			/**
			 * Triggers events aggregation on the <code>TextChangedEventBuffer</code>
			 * if the user is saving his changes. This is to ensure that all the 
			 * changes have arrived on the server before the text file is saved.
			 */ 
			public function emptyKeystrokeAggregationBuffer():void {
				if (keystrokeBuffer)
					keystrokeBuffer.aggregateEvents();
			}
			
			public function setKeystrokeAggregationBufferDirtyState(dirty:Boolean):void {
				if (keystrokeBuffer)
					keystrokeBuffer.dirty = dirty;
			}
			
			protected function initializeHandler(event:FlexEvent):void {
				if (FlexUtilGlobals.getInstance().isMobile) {					
					editor = new MobileCodeMirrorEditor();						
				} else {
					editor = UIComponent(new WebCodeMirrorEditor("editor"));					
				}					
			}
			
			override protected function creationCompleteHandler(event:FlexEvent):void {		
				super.creationCompleteHandler(event);
												
				ICodeMirrorEditor(editor).load(FlexUtilGlobals.getInstance().createAbsoluteUrl(EditorTextPlugin.getInstance().getResourceUrl(getURL())));
				ICodeMirrorEditor(editor).addViewCompleteHandler(orionEditor_frameLoadHandler);
				
				creationComplete = true;
				setKeystrokeAggregationInterval(_keystrokeAggregationInterval);
				addEventListener(BufferedTextChangesEvent.BUFFERED_TEXT_CHANGES, sendTextEditorChangesToServer);
			}
			
			protected function getURL():String {
				var lastDotIndex:int = editorStatefulClient.editableResourcePath.lastIndexOf('.');
				if (lastDotIndex >= 0) {
					// has an extension
					var extension:String = editorStatefulClient.editableResourcePath.substring(lastDotIndex + 1);
					return EDITOR_PAGE + "?extension=" + extension + "&isMobile=" + FlexUtilGlobals.getInstance().isMobile;
				}
				return EDITOR_PAGE;
			}
			
			protected function orionEditor_frameLoadHandler(event:Event):void {
				// initialize code mirror editor
				ICodeMirrorEditor(editor).callJavaScriptMethod("initialize", null, 
					content != null ? escape(content) : null, editorStatefulClient.getStatefulClientId(), frontendId, readOnly);					
				
				// listen for text changes
				ICodeMirrorEditor(editor).addCallbackHandler("codeMirrorEditorChangedHandler", textEditorChangedHandler);
			}
			
			public function setContent(content:String):void {
				ICodeMirrorEditor(editor).callJavaScriptMethod("setContent", null, escape(content));
			}
			
			public function getContent(callback:Function):void {
				ICodeMirrorEditor(editor).callJavaScriptMethod("getContent", callback, null);
			}
			
			override public function disableEditing():void {
				if (creationComplete) {
					ICodeMirrorEditor(editor).callJavaScriptMethod("disableEditing");
				} else {
					readOnly = true;
				}				
			}
			
			override public function enableEditing():void {
				if (creationComplete) {
					ICodeMirrorEditor(editor).callJavaScriptMethod("enableEditing");
				} else {
					readOnly = false;
				}				
			}
			
			protected function updateText(offset:int, oldTextLength:int, newText:String):void {
				ICodeMirrorEditor(editor).callJavaScriptMethod("updateText", null, offset, oldTextLength, escape(newText));
			}
			
			override public function executeContentUpdateLogic(content:Object, isFullContent:Boolean):void {				
				var updates:ArrayCollection = ArrayCollection(content);
				if (isFullContent) {
					// Taking the first because probably only one exists with fullText.
					var update:TextEditorUpdate = TextEditorUpdate(updates[0]); 
					this.content = update.newText;
				} else { 
					// apply the updates
					for each (var update:TextEditorUpdate in updates) {						
						updateText(update.offset, update.oldTextLength, update.newText);
					}
				}
			}	
				
			protected function textEditorChangedHandler(statefulClientId:String, frontendId:String, offset:String, oldText:String, newText:String):void {				
				// here we must know where to send those updates
				// ind the statefulClient based on the id sent at code mirror initialisation
				var editorStatefulClient:EditorStatefulClient = 
					EditorStatefulClient(CommunicationPlugin.getInstance().statefulClientRegistry.getStatefulClientById(statefulClientId));
				
				for each (var editorFrontend:EditorFrontend in editorStatefulClient.editorFrontends) {
					var codeMirrorEditor:CodeMirrorEditorFrontend = CodeMirrorEditorFrontend(editorFrontend);
					if (codeMirrorEditor.frontendId == frontendId) {
						codeMirrorEditor.keystrokeBuffer.add(new TextChangedEvent(int(offset), oldText.length, newText));		
						break;
					}
				}	
			}
						
			private function sendTextEditorChangesToServer(evt:BufferedTextChangesEvent):void {			
				var buffer:ArrayCollection = evt.buffer;
				
				var updates:ArrayCollection = new ArrayCollection();
				for each (var textChangedEvent:TextChangedEvent in buffer) {
					var update:TextEditorUpdate = new TextEditorUpdate();
					update.offset = textChangedEvent.offset;
					update.oldTextLength = textChangedEvent.oldTextLength;
					update.newText = textChangedEvent.newText;
					updates.addItem(update);
				}
				
				editorStatefulClient.attemptUpdateContent(this, updates);
			}
			
		]]>
	</mx:Script>	
	
</editorsupport:EditorFrontend>