<?xml version="1.0" encoding="utf-8"?>
<popup:Wizard xmlns:fx="http://www.adobe.com/2006/mxml" 
			  xmlns:s="library://ns.adobe.com/flex/spark" 
			  xmlns:mx="library://ns.adobe.com/flex/mx" 
			  xmlns:popup="org.flowerplatform.flexutil.popup.*"
			  
			  getIndexForStateFunction="getIndexForState" 
			  
			  xmlns:component="org.flowerplatform.web.git.common.ui.component.*" 
			  xmlns:common="com.crispico.flower.flexdiagram.util.common.*">
	
	<fx:Script>
		<![CDATA[
			import com.crispico.flower.util.spinner.ModalSpinner;
			
			import mx.collections.ArrayCollection;
			
			import org.flowerplatform.common.CommonPlugin;
			import org.flowerplatform.communication.tree.remote.TreeNode;
			import org.flowerplatform.flexutil.FlexUtilGlobals;
			import org.flowerplatform.flexutil.popup.IPopupHost;
			import org.flowerplatform.web.git.common.GitCommonPlugin;
			import org.flowerplatform.web.git.common.remote.dto.GitRef;
			
			public var selectedNode:TreeNode;
			
			private var allBranches:ArrayCollection;
			
			[Bindable]
			private var branches:ArrayCollection = new ArrayCollection(); /* of GitRef */
			
			private var head:GitRef;
			
			[Bindable]
			private var defaultRemoteName:String = "origin";
			
			override protected function creationCompleteHandler():void {
				super.creationCompleteHandler();		
				wizardImage.source = GitCommonPlugin.getInstance().getResourceUrl("images/import_wiz.png");
				titleArea.height = 75;
				allBranches = new ArrayCollection();
				//it doesn't work by simply apply focus
				callLater(repositorySelectionPage.uri.setFocus);
			}	
			
			/**
			 * @flowerModelElementId _gvgoEDVgEeKsnMbt6YrXWQ
			 */
			protected override function nextClickHandler():void {
				if (currentState == "repositoryURIPage") {
					//ModalSpinner.addModalSpinner(this, GitCommonPlugin.getInstance().getMessage("git.cloneWizard.validate.spinner"));
					GitCommonPlugin.getInstance().service.
						validateRepositoryURL(selectedNode, repositorySelectionPage.getURI(), this, validateRepositoryCallbackHandler);					
				} else if (currentState == "branchesPage") {
					var selectedBranches:ArrayCollection = getSelectedBranches();
					// at least one branch must be selected in order to move further
					if (allBranches.length > 0 && selectedBranches.length == 0) {
						FlexUtilGlobals.getInstance().messageBoxFactory.createMessageBox()
							.setTitle(CommonPlugin.getInstance().getMessage("error"))
							.setText(GitCommonPlugin.getInstance().getMessage("git.cloneWizard.noBranchSelected.error"))
							.showMessageBox();
					} else {
						super.nextClickHandler();						
					}
				}
			}
			
			/**
			 * @flowerModelElementId _mCRiMDVgEeKsnMbt6YrXWQ
			 */
			private function validateRepositoryCallbackHandler(value:int):void {
				//ModalSpinner.removeModalSpinner(this);
				switch (value) {
					case 1:
						FlexUtilGlobals.getInstance().messageBoxFactory.createMessageBox()
						.setTitle(CommonPlugin.getInstance().getMessage("error"))
						.setText(GitCommonPlugin.getInstance().getMessage("git.cloneWizard.repoExists.error"))
						.showMessageBox();
						break;
					case 0:
						//ModalSpinner.addModalSpinner(this, CommonPlugin.getInstance().getMessage("spinner.gettingData"));
						GitCommonPlugin.getInstance().service.
						getBranches(repositorySelectionPage.getURI(), this, getBranchesCallbackHandler);
				}				
			}
			
			private function getBranchesCallbackHandler(data:ArrayCollection):void {
				super.nextClickHandler();
				
				if (data != null) {
					allBranches = ArrayCollection(data[0]);					
					head = GitRef(data[1]);					
				} else {
					allBranches.removeAll();
					head = null;					
				}
				allBranches.refresh();
				
				applyFilterHandler();
				
				//ModalSpinner.removeModalSpinner(this);
			}
			
			private function getSelectedBranches(onlyName:Boolean = false):ArrayCollection {
				var list:ArrayCollection = new ArrayCollection();
				for each (var branch:GitRef in branches) {
					if (branch.getSelected()) {
						list.addItem(onlyName ? branch.name : branch);
					}
				}
				return list;
			}
			
			protected function applyFilterHandler():void {
				branches.removeAll();
				for each (var branch:GitRef in allBranches) {
					if (filter.text == GitCommonPlugin.getInstance().getMessage('git.cloneWizard.branch.filter')) {
						branches.addItem(branch);
					} else if (branch.shortName.toLowerCase().match(filter.text.toLowerCase())) {
						branches.addItem(branch);
					}
				}
				branches.refresh();
			}
			
			/**
			 * @flowerModelElementId _0LbYwDVhEeKsnMbt6YrXWQ
			 */
			protected override function finishClickHandler():void {
				//ModalSpinner.addModalSpinner(this, GitCommonPlugin.getInstance().getMessage("git.cloneWizard.cloneRepo.spinner"));
				var selectedBranches:ArrayCollection = getSelectedBranches(true);				
				GitCommonPlugin.getInstance().service.
					cloneRepository(selectedNode,
						repositorySelectionPage.getURI(),
						selectedBranches,
						remoteName != null ? remoteName.text : defaultRemoteName,
						allBranches.length == 0 || selectedBranches.length == allBranches.length,
						this, serverResponseHandler);
			}
			
			private function serverResponseHandler(value:Boolean):void {
			//	ModalSpinner.removeModalSpinner(this);
				if (value) {
					super.finishClickHandler();
				}
			}
			
			override public function set popupHost(value:IPopupHost):void {
				super.popupHost = value;
				popupHost.setLabel(GitCommonPlugin.getInstance().getMessage('git.action.cloneRepo.label'));
				popupHost.setIcon(GitCommonPlugin.getInstance().getResourceUrl('images/full/obj16/cloneGit.gif'));
			}
			
			private function getIndexForState(stateName:String):int {
				if (stateName == "repositoryURIPage") {
					return 0;
				}
				return 1;
			}
			
		]]>
	</fx:Script>
	
	<popup:states>				
		<mx:State name="repositoryURIPage">			
			<mx:SetProperty name="wizardTitle" value="{GitCommonPlugin.getInstance().getMessage('git.repoSelection.title')}"/>					
			<mx:SetProperty name="wizardDescription" value="{GitCommonPlugin.getInstance().getMessage('git.repoSelection.description')}"/>
			
			<mx:AddChild relativeTo="{contentArea}">
				<component:RepositorySelectionPage id="repositorySelectionPage" width="100%" height="100%" />	
			</mx:AddChild>
		</mx:State>
		
		<mx:State name="branchesPage" >			
			<mx:SetProperty name="wizardTitle" value="{GitCommonPlugin.getInstance().getMessage('git.cloneWizard.cloneConfig.title')}"/>					
			<mx:SetProperty name="wizardDescription" value="{GitCommonPlugin.getInstance().getMessage('git.cloneWizard.branch.description')}"/>			
			
			<mx:AddChild relativeTo="{contentArea}">
				<mx:VBox width="100%" height="100%" paddingTop="10" borderVisible="false">
					<mx:Label text="{GitCommonPlugin.getInstance().getMessage('git.cloneWizard.branch.branchOf', [repositorySelectionPage.getURI()])}"/>
					<s:TextInput id="filter" width="100%" 
								  change="applyFilterHandler()" />
								<!--  creationComplete="{TextUtils.setTextComponentHint(filter, GitCommonPlugin.getInstance().getMessage('git.cloneWizard.branch.filter'));}"/-->
					<mx:List id="listBranches" width="100%" height="100%" dataProvider="{branches}"  >
						<mx:itemRenderer>
							<fx:Component>
								<mx:HBox>
									<fx:Script>
										<![CDATA[								
											import org.flowerplatform.web.git.common.GitCommonPlugin;
											import org.flowerplatform.web.git.common.remote.dto.GitRef;
										
										
										]]>
									</fx:Script>
									
									<mx:CheckBox click="{GitRef(data).setSelected(!GitRef(data).getSelected())}" selected="{GitRef(data).getSelected()}" />									
									<common:BitmapContainer id="image" creationComplete="{image.retrieveImage(GitCommonPlugin.getInstance().getResourceUrl('images/full/obj16/branch_obj.gif'))}"/>	
									<s:Label text="{data.shortName}" />
								</mx:HBox>					
							</fx:Component>
						</mx:itemRenderer>
					</mx:List>
					<mx:HBox paddingTop="10" width="100%">
						<s:Label text="{GitCommonPlugin.getInstance().getMessage('git.cloneWizard.cloneConfig.remoteName')}"/>
						<s:TextInput id="remoteName" width="100%" text="{defaultRemoteName}" />
					</mx:HBox>
				</mx:VBox>
			</mx:AddChild>
		</mx:State>		
	</popup:states>
	
</popup:Wizard>
