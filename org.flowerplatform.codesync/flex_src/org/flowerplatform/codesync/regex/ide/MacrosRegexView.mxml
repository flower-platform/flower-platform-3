<?xml version="1.0" encoding="utf-8"?>
<!-- 
license-start

Copyright (C) 2008 - 2013 Crispico, <http://www.crispico.com/>.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation version 3.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details, at <http://www.gnu.org/licenses/>.

Contributors:
Crispico - Initial API and implementation  The product is licensed under GPL. This is the new version of the license.

license-end
-->
<!-- 
@author Cristina Constatinescu
-->
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  implements="org.flowerplatform.flexutil.view_content_host.IViewContent, org.flowerplatform.flexutil.selection.ISelectionProvider, org.flowerplatform.flexutil.selection.ISelectionForServerProvider"
		  creationComplete="creationCompleteHandler(event)" paddingTop="5" paddingBottom="5">
				

	<fx:Script>
		<![CDATA[
			import com.crispico.flower.util.layout.event.ViewAddedEvent;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.collections.IList;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			
			import org.flowerplatform.codesync.CodeSyncPlugin;
			import org.flowerplatform.codesync.regex.ide.action.AddMacroRegexAction;
			import org.flowerplatform.codesync.regex.ide.action.RemoveMacroRegexAction;
			import org.flowerplatform.codesync.regex.ide.remote.RegexSelectedItem;
			import org.flowerplatform.codesync.regex.ide.ui.RegexItemRenderer;
			import org.flowerplatform.communication.CommunicationPlugin;
			import org.flowerplatform.communication.service.InvokeServiceMethodServerCommand;
			import org.flowerplatform.emf_model.regex.MacroRegex;
			import org.flowerplatform.flexutil.FlexUtilGlobals;
			import org.flowerplatform.flexutil.action.IAction;
			import org.flowerplatform.flexutil.layout.event.ViewRemovedEvent;
			import org.flowerplatform.flexutil.view_content_host.IViewHost;
			
			protected var _viewHost:IViewHost;
			
			public function getActions(selection:IList):Vector.<IAction> {			
				var result:Vector.<IAction> = new Vector.<IAction>();
				result.push(new AddMacroRegexAction());		
				result.push(new RemoveMacroRegexAction(this));
				return result;
			}
			
			public function set viewHost(viewHost:IViewHost):void {
				if (_viewHost != null) {
					DisplayObject(_viewHost).removeEventListener(ViewAddedEvent.VIEW_ADDED, viewAddedHandler);
					DisplayObject(_viewHost).removeEventListener(ViewRemovedEvent.VIEW_REMOVED, viewRemovedHandler);	
				}
				
				_viewHost = viewHost;
				
				DisplayObject(_viewHost).addEventListener(ViewAddedEvent.VIEW_ADDED, viewAddedHandler);
				DisplayObject(_viewHost).addEventListener(ViewRemovedEvent.VIEW_REMOVED, viewRemovedHandler);	
			}
			
			public function getSelection():IList {			
				var array:ArrayList = new ArrayList();
				for (var i:int = 0; i < list.selectedItems.length; i++) {
					array.addItem(list.selectedItems[i]);
				}
				return array;
			}
			
			private function viewAddedHandler(event:ViewAddedEvent):void {
				// show actions in buttonBar
				_viewHost.selectionChanged();
				FlexGlobals.topLevelApplication.addEventListener(RegexDataEvent.SELECTED_CONFIG_CHANGED, selectedConfigChangedHandler);
				FlexGlobals.topLevelApplication.addEventListener(RegexDataEvent.MACROS_REQUEST_REFRESH, refreshHandler);
			}
						
			private function viewRemovedHandler(event:ViewRemovedEvent):void {			
				FlexGlobals.topLevelApplication.removeEventListener(RegexDataEvent.SELECTED_CONFIG_CHANGED, selectedConfigChangedHandler);
				FlexGlobals.topLevelApplication.removeEventListener(RegexDataEvent.MACROS_REQUEST_REFRESH, refreshHandler);
			}
			
			private function selectedConfigChangedHandler(event:RegexDataEvent):void {
				if (CodeSyncPlugin.getInstance().regexUtils.selectedConfig == null) {
					list.dataProvider.removeAll();
				} else {
					refreshHandler();
				}
			}
			
			public function convertSelectionToSelectionForServer(selection:IList):IList {
				if (selection == null) {
					return selection;
				}			
				var selectedItems:ArrayCollection = new ArrayCollection();
				for each (var regex:MacroRegex in selection.toArray()) {
					var selectedItem:RegexSelectedItem = new RegexSelectedItem();
					selectedItem.regex = regex;
					selectedItem.config = CodeSyncPlugin.getInstance().regexUtils.selectedConfig;
					selectedItem.itemType = "regex_macro";
					selectedItems.addItem(selectedItem);
				}
				
				return selectedItems;
			}
						
			protected function creationCompleteHandler(event:FlexEvent):void {
				list.itemRenderer = new ClassFactory(RegexItemRenderer);
				ClassFactory(list.itemRenderer).properties = {iconFunction: getIcon};
				
				list.addEventListener(FlexEvent.VALUE_COMMIT, changeHandler);
			}
			
			private function getIcon(item:Object):Object {
				return CodeSyncPlugin.getInstance().getResourceUrl("images/regex/bullet_star.png");
			}
			
			public function refreshHandler(event:RegexDataEvent = null):void {
				CommunicationPlugin.getInstance().bridge.sendObject(
					new InvokeServiceMethodServerCommand("regexService", "getMacros", [CodeSyncPlugin.getInstance().regexUtils.selectedConfig], 
						this, getMacrosRegexesCallbackHandler));
			}
			
			public function getMacrosRegexesCallbackHandler(result:ArrayCollection):void {
				if (result != null) {
					var oldSelectedItem:String = list.selectedItem;
					
					list.dataProvider = ArrayCollection(result);
					
					if (list.dataProvider.getItemIndex(oldSelectedItem) != -1) {
						list.selectedItem = oldSelectedItem;
					}
				}
			}
			
			private function changeHandler(event:Event):void {	
				FlexUtilGlobals.getInstance().selectionManager.selectionChanged(_viewHost, this);
				
				event.stopImmediatePropagation();
			}
		]]>
	</fx:Script>
	
	<s:Label text="{CodeSyncPlugin.getInstance().regexUtils.selectedConfig != null ? CodeSyncPlugin.getInstance().getMessage('regex.selectedConfig', [CodeSyncPlugin.getInstance().regexUtils.selectedConfig]) : CodeSyncPlugin.getInstance().getMessage('regex.selectedConfig.none')}" 
			 width="100%" paddingLeft="5" fontWeight="bold"/>
	<s:List id="list" width="100%" height="100%" labelField="name" />
	
</s:VGroup>
